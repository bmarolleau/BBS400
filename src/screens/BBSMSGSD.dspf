     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Post')
     A                                      CF12(12 'Go back')
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPTITLE('BBS400 MSGL')
     A                                      HLPSCHIDX(*LIBL/BBSHELPPNL)
     A*****************************************************************
     A          R FOOTER
     A                                      OVERLAY
     A                                 23  2'F5=Refresh   F6=Post message   F12-
     A                                      =Go back'
     A                                      COLOR(BLU)
     A            MSGLIN        78A  O 24  2DSPATR(HI)
     A*****************************************************************
     A          R SF                        SFL
     A            SCROPT         1A  B 12  3VALUES(' ' '5' '7')
     A  43                                  COLOR(RED)
     A            SCRSBJ        45A  O 12  6
     A  43                                  COLOR(RED)
     A            SCRDAT         8A  O 12 52
     A  43                                  COLOR(RED)
     A            SCRTIM         8A  O 12 61
     A  43                                  COLOR(RED)
     A            SCRSND        10A  O 12 71
     A  43                                  COLOR(RED)
     A  44                                  COLOR(WHT)
     A            SCRMUI        32A  H
     A*****************************************************************
     A          R SFLCTL                    SFLCTL(SF)
     A                                      OVERLAY
     A  40                                  SFLDSP
     A                                      SFLDSPCTL
     A  41                                  SFLCLR
     A  42                                  SFLEND(*MORE)
     A                                      SFLSIZ(9999)
     A                                      SFLPAG(10)
     A                                      HLPCLR
     A          H                           HLPPNLGRP(MSGL BBSHELPPNL)
     A                                      HLPARA(01 001 02 080)
     A            SCRSCR        10A  O  1  3
     A                                  1 26'BBS400 - Sub-Board''s Message List'
     A                                      DSPATR(HI)
     A            SCRBBS        40A  O  2  3
     A                                      COLOR(RED)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A            SCRNCK        10A  O  3  3
     A                                      COLOR(BLU)
     A                                      DSPATR(RI)
     A                                  3 15'Level:'
     A            SCRLVL         2Y 0O  3 22
     A                                      COLOR(BLU)
     A            SCRLVD        15A  O  3 25
     A                                      COLOR(WHT)
     A                                  5  2'Board . . :'
     A                                      DSPATR(HI)
     A            SCRBRS         8A  O  5 14
     A            SCRBRL        30A  O  5 24
     A                                  6  2'Sub-Board :'
     A                                      DSPATR(HI)
     A            SCRSBS         8A  O  6 14
     A            SCRSBL        30A  O  6 24
     A                                  8  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  9  3'5=Display   7=Reply'
     A                                      COLOR(BLU)
     A                                 11  2'Opt Subject                       -
     A                                                      Date     Time      -
     A                                      Posted by'
     A                                      DSPATR(HI)
