     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF10(10)
     A                                      CF12(12)
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPTITLE('BBS400 SYSINF')
     A                                      HLPSCHIDX(*LIBL/BBSHELPPNL)
     A*****************************************************************
     A          R HEADER
     A                                      OVERLAY
     A            SCRSCR        10A  O  1  3
     A                                  1 26'BBS400 - System Information'
     A                                      DSPATR(HI)
     A            SCRBBS        40A  O  2  3
     A                                      COLOR(RED)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A            SCRNCK        10A  O  3  3
     A                                      COLOR(BLU)
     A                                      DSPATR(RI)
     A                                  3 15'Level:'
     A            SCRLVL         2Y 0O  3 22COLOR(BLU)
     A            SCRLVD        15A  O  3 25COLOR(WHT)
     A*****************************************************************
     A          R BODY
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(SYSINF BBSHELPPNL)
     A                                      HLPARA(01 001 02 080)
     A                                  6  4'Model  . . . :'
     A            SCRMDL         4A  O  6 19
     A                                  8  4'Last IPL . . :'
     A            SCRLID         8A     8 19
     A            SCRLIT         8A     8 29
     A                                 10  4'Current Date :'
     A                                 10 19DATE
     A                                      EDTCDE(Y)
     A                                 10 70TIME
     A                                 11  4'Date Format  :'
     A            SCRDFM         3A  O 11 19
     A                                 13  4'Country ID . :'
     A            SCRCID         2A  O 13 19
     A                                  6 50'Processor feature :'
     A            SCRPFE         4A  O  6 70
     A                                 10 50'Current Time  . . :'
     A                                 11 50'UTC offset  . . . :'
     A            SCRUOF         5   O 11 70
     A*****************************************************************
     A          R FOOTER
     A                                      OVERLAY
     A                                 23  2'F12=Go back'
     A                                      COLOR(BLU)
     A            MSGLIN        78A  O 24  2DSPATR(HI)
