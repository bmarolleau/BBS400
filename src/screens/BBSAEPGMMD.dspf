     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF10(10 'Save changes')
     A                                      CF12(12 'Go back')
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPTITLE('BBS400 EPGMSA')
     A                                      HLPSCHIDX(*LIBL/BBSHELPPNL)
     A*****************************************************************
     A          R HEADER
     A                                      OVERLAY
     A            SCRSCR        10A  O  1  3
     A            SCRNAM        35A  O  1 28DSPATR(HI)
     A            SCRBBS        40A  O  2  3
     A                                      COLOR(RED)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A            SCRNCK        10A  O  3  3
     A                                      COLOR(BLU)
     A                                      DSPATR(RI)
     A                                  3 15'Level:'
     A            SCRLVL         2Y 0O  3 22COLOR(BLU)
     A            SCRLVD        15A  O  3 25
     A                                      COLOR(WHT)
     A*****************************************************************
     A          R BODY
     A                                      OVERLAY
     A                                      HLPCLR
     A          H                           HLPPNLGRP(EPGMSA BBSHELPPNL)
     A                                      HLPARA(01 001 02 080)
     A                                  5  3'Description . . . :'
     A            SCRPNM        30A  B  5 23CHECK(LC)
     A                                      DSPATR(HI)
     A                                  7  3'Program (OBJ) . . :'
     A            SCROBJ        10A  B  7 23DSPATR(HI)
     A                                  8  3'Program (LIB) . . :'
     A            SCRLIB        10A  B  8 23DSPATR(HI)
     A                                 10  3'Menu Order  . . . :'
     A            SCRORD         2Y 0B 10 23DSPATR(HI)
     A                                 12  3'Min. Access Level :'
     A            SCRALV         2Y 0B 12 23DSPATR(HI)
     A*****************************************************************
     A          R FOOTER
     A                                      OVERLAY
     A                                 23  2'F10=Confirm changes   F12=Go back'
     A                                      COLOR(BLU)
     A            MSGLIN        78A  O 24  2DSPATR(HI)
